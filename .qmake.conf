# sets projectwide variables and implicitely
# enables qmake to lookup the projectroot/features
# directory for possible feature files
#
# Any variable specified here, must be set manually
# inside the debian/rules file

load(qt_build_config)
CONFIG += warning_clean

MODULE_VERSION = 5.5.0

ROOT_SOURCE_DIR=$$PWD
ROOT_BUILD_DIR=$$shadowed($$PWD)

exists($$PWD/../qt.pro) {
    CONFIG += build_with_qt
}

*-g++* {
    GCC4 = $$system( gcc -dumpversion | grep "^4.[0-9]" )
    if (isEmpty(GCC4)) {
        QMAKE_CXXFLAGS += -Wsuggest-override
    }
}

# build UbuntuToolkit without namespace
# DEFINES += UBUNTUTOOLKIT_NO_NAMESPACE
# build UbuntuGestures without namespace
# DEFINES += UBUNTUGESTURES_NO_NAMESPACE
