# sets projectwide variables and implicitely
# enables qmake to lookup the projectroot/features
# directory for possible feature files
#
# Any variable specified here, must be set manually
# inside the debian/rules file

load(qt_build_config)
CONFIG += warning_clean
CONFIG += c++11

MODULE_VERSION = 5.5.0

ROOT_SOURCE_DIR=$$PWD
ROOT_BUILD_DIR=$$shadowed($$PWD)

exists($$PWD/../qt.pro) {
    CONFIG += build_with_qt
} else {
    # When built as a Qt submodule, -Wsuggest-override breaks the build. That's
    # fixed in Qt 5.6. See https://codereview.qt-project.org/#/c/169969/
    gcc:greaterThan(QT_GCC_MAJOR_VERSION, 4): QMAKE_CXXFLAGS += -Wsuggest-override
}

# FIXME: Fix bug 1606247 and enable the UbuntuToolkit namespace.
# build UbuntuToolkit without namespace
DEFINES += UBUNTUTOOLKIT_NO_NAMESPACE
# build UbuntuGestures without namespace
# DEFINES += UBUNTUGESTURES_NO_NAMESPACE
